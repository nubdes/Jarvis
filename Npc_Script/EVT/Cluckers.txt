//===== Hercules Script =======================================
//= Cluck! Cluck! Boom!
//===== By: ==================================================
//= Keale
//===== Current Version: =====================================
//= 1.2a
//===== Description: =========================================
//= Click the chicken and try retrieve the item at a low
//= chance. If you fail he will nuke, freeze, stone,
//= stun, or make you fall asleep.
//= The prize is configurable and triggered by the NPC.
//===== Additional Comments: =================================
//= 1.0 First release.
//= 1.1 Using 'switch rand' instead.
//= 1.2 Cleaned and standardized. [Euphy]
//= 1.2a Switched 'atcommand' to 'unitskilluseid'.
//============================================================

midgard3,199,224,3	script	Cluckers	4_NFCOCK,{

	set .@GMAccess,4;	// GM level required to access NPC

	if (.startcluck) {
		specialeffect(EF_HIT3, AREA, playerattached());
		switch(rand(15)) {
			case 0: npctalk "CLUUUUUUCK!!!"; unitskilluseid getcharid(CHAR_ID_ACCOUNT),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 1: npctalk "Cluuuuuck!~"; break;
			case 2: unitskilluseid getcharid(CHAR_ID_ACCOUNT),"NPC_SELFDESTRUCTION",1; break;
			case 3: sc_start SC_FREEZE,10000,0; break;
			case 4: npctalk "CLUUUUUUUUUCK!!!"; unitskilluseid getcharid(CHAR_ID_ACCOUNT),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 5: sc_start SC_SLEEP,10000,0; break;
			case 6: sc_start SC_STONE,10000,0; emotion 29; break;
			case 7: npctalk "CLUUUUUUCK!!!"; unitskilluseid getcharid(CHAR_ID_ACCOUNT),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 8: npctalk "Cluck! CLUUUCK!!"; unitskilluseid getcharid(CHAR_ID_ACCOUNT),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 9: sc_start SC_STUN,10000,0; break;
			case 10: sc_start SC_SLEEP,10000,0; emotion 29; break;
			case 11: npctalk "Cluck! Cluck!"; break;
			case 12: sc_start SC_STUN,10000,0; break;
			case 13: unitskilluseid getcharid(CHAR_ID_ACCOUNT),"NPC_SELFDESTRUCTION",1; break;
			default:
				if (rand(50) < 4) {
					npctalk "WOOF!...........";
					specialeffect(EF_SPHERE, AREA, playerattached());
					announce "[Cluck! Cluck! Boom!] " + strcharinfo(PC_NAME) + " squeezed out the prize! Well done!",0,0x64E986;
					getitem 8900,5;
					set .startcluck,0;
				} else {
					npctalk "Cluck! CLUUUCK!!";
					unitskilluseid getcharid(CHAR_ID_ACCOUNT),"NPC_SELFDESTRUCTION",1;
				}
				break;
		}
		end;
	}
	mes "[Cluckers]";
	if (getgmlevel() >= .@GMAccess) {
		mes "Cluck cluck! Cluuuuuck? ^FF0000~Hi GM " + strcharinfo(PC_NAME) + ", Wanna play today?~^000000";
		mes "Cluck cluck... CLUCK! ^FF0000~Just tell me what to do!~^000000";
		next;
		while(1) switch(select("Start Event", "Check Prize", "Set Prize", "Not today Cluckers")) {
			case 1:
				mes "[Cluckers]";
				mes "CLUCK! ^FF0000~Sure thing!~^000000";
				emotion 33;
				close2;
				announce "[Cluck! Cluck! Boom!] is about to start in midgard3 199 224",0,0x64E986;
				initnpctimer;
				end;
			case 2:
				mes "[Cluckers]";
				mes "Cluck, cluck cluck... Cluck! ^FF0000~The current prize is^000000 ^008000"+$Cluck_Amount_P+"^000000";
				next;
				break;
			case 3:
				mes "[Cluckers]";
				mes "Cluck? cluuuck? ^FF0000~How many event ponts should i give away?~^000000";
				input $Cluck_Amount_P;
				next;
				mes "[Cluckers]";
				mes "Cluck cluck..? Cluck. ^FF0000~So, the prize is^000000 ^008000"+$cluck_item_id+", ^008000"+$Cluck_Amount_P+"^000000? ^FF0000Great.~^000000";
				emotion 33;
				next;
				break;
			case 4:
				mes "[Cluckers]";
				mes "Cluck cluck cluck...";
				close;
		}
	}
	mes "Cluck cluck! Cluuuuuck?";
	mes "Cluck....";
	close;

OnTimer10000:
	announce "Please hurry @ prontera 156 128 if you want to play with the crazy chicken!",0,0x64E986;
	end;
OnTimer20000:
	announce "Cluckers has eaten one of my items! I'm too scared to retrieve it!",0,0x64E986;
	end;
OnTimer30000:
	announce "Click the insane chicken and try squeeze out the item, if you're lucky you'll win! Are you ready?",0,0x64E986;
	end;
OnTimer40000:
	announce "GO! Click the chicken to get the prize!",0,0x64E986;
	stopnpctimer;
	if (!$cluck_item_id) set $cluck_item_id,8900;
	if (!$Cluck_Amount_P) set $Cluck_Amount_P,5;
	set .startcluck,1;
	end;

OnHour00:
OnHour04:
OnHour08:
OnHour12:
OnHour16:
OnHour20:
	if(agitcheck()||agitcheck2()) end;
	initnpctimer;
	end;

}